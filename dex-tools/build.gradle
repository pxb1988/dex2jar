plugins {
    id "application"
}

description = 'Dex Tools'

dependencies {
    implementation project(':dex-translator')
    implementation project(':d2j-smali')
    implementation project(':d2j-jasmin')
    implementation project(':dex-writer')
    implementation project(':dex-ir')
    implementation project(':dex-reader-api')
    implementation project(':dex-reader')
    implementation project(':d2j-base-cmd')
    implementation fileTree(dir: '../libs', include: '*.jar')
    // implementation group: 'org.ow2.asm', name: 'asm', version: project.property('asmVersion') // Fixed MethodCodeTooLargeException
    implementation(group: 'org.ow2.asm', name: 'asm-analysis', version: project.property('asmVersion')) {
        exclude(module: 'asm')
    }
    implementation(group: 'org.ow2.asm', name: 'asm-commons', version: project.property('asmVersion')) {
        exclude(module: 'asm')
    }
    implementation(group: 'org.ow2.asm', name: 'asm-tree', version: project.property('asmVersion')) {
        exclude(module: 'asm')
    }
    implementation(group: 'org.ow2.asm', name: 'asm-util', version: project.property('asmVersion')) {
        exclude(module: 'asm')
    }
}

task bin_gen(type: JavaExec) {
    dependsOn jar
    classpath sourceSets.main.runtimeClasspath
    ext.binDir = "$buildDir/generated-sources/bin"
    outputs.dir file(ext.binDir)
    mainClass.set('com.googlecode.dex2jar.bin_gen.BinGen')
    args = ["$projectDir/src/main/bin_gen", "$ext.binDir"]
}
applicationDistribution.from(bin_gen)
//applicationDistribution.from('src/main/jars') {
//  into("lib")
//}
applicationDistribution.from('open-source-license.txt') {
    into("lib")
}
applicationDistribution.from("$parent.rootDir") {
    include("NOTICE.txt")
    include("LICENSE.txt")
}

startScripts.dependsOn bin_gen

artifacts.archives distZip
